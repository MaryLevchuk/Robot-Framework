*** Keywords ***
Update recipe translation
    [Arguments]    ${field_name}    ${value}
    ${updated_field}=    Run Keyword IF    ${value} == -1    Remove recipe field    ${field_name}
    ...    ELSE IF    ${value} == ${null}    Update recipe field    /${field_name}    null
    ...    ELSE    Update recipe field    /${field_name}    ${value}
    ${status}=    Send recipe translation    ${updated_field}
    Should Start With    ${status}    200

Update recipe field
    [Arguments]    ${key}    ${value}
    Log    UPDATING RECIPE FIELD
    ${translation}=    Get recipe translation part
    ${translated_json}=    Set Json Value    ${translation}    ${key}    ${value}
    [Return]    ${translated_json}

Get recipe to translate by ID
    [Arguments]    ${recipe_id}
    Set Request Headers
    Log    GETTING RECIPE TO TRANSLATE BY ID
    GET    /${instance}/translation/recipe/${recipe_id}
    ${response_body}=    Get Response Body
    [Return]    ${response_body}

Get recipe translation part
    Log    TAKING RECIPE TRANSLATION PART
    ${response_body}=    Get recipe to translate by ID    ${recipe_id}
    ${translation_part}=    Get Json Value    ${response_body}    /TranslationRecipe
    [Return]    ${translation_part}

Get all recipes to translate
    Set Request Headers
    GET    /${instance}/translation/recipe
    ${response_body}=    Get Response Body
    [Return]    ${response_body}

Remove recipe field
    [Arguments]    ${field_name}
    Log    "I'm in Remove field"
    ${translation_part}=    Get recipe translation part
    ${json_string}=    catenate    ${translation_part}
    ${json}=    evaluate    json.loads('''${json_string}''')    json
    set to dictionary    ${json}
    remove from dictionary    ${json}    ${field_name}
    ${json_string}=    evaluate    json.dumps(${json})    json
    Comment    log    \nNew JSON string:\n${json_string}
    [Return]    ${json_string}

Send recipe translation
    [Arguments]    ${json_data}
    Set Request Headers
    Log    SENDING RECIPE TRANSLATION PART
    ${json_string}=    catenate    ${json_data}
    ${bytes}=    Encode String To Bytes    ${json_string}    ASCII    errors=ignore
    Set Request Body    ${bytes}
    POST    /${instance}/translation/recipe/${recipe_id}
    ${status}=    Get Response Status
    ${body}=    Get Response Body
    [Return]    ${status}
